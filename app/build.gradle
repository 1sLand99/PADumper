plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
}

//https://gist.github.com/paramsen/4a92c9230d799caeb3b90897da27a80c
static def generateVersionCode() {
    def result = "git rev-list HEAD --count".execute().text.trim() //unix
    if(result.empty) result = "PowerShell -Command git rev-list HEAD --count".execute().text.trim() //windows
    if(result.empty) throw new RuntimeException("Could not generate versioncode on this platform? Cmd output: ${result.text}")
    return result.toInteger()
}

android {
    compileSdk 34
    namespace "com.dumper.android"

    defaultConfig {
        applicationId  "com.dumper.android"
        minSdk 21
        targetSdk 34
        versionCode generateVersionCode()
        versionName "0.0.7"
    }

    signingConfigs {
        debug {
            storeFile file("keystore.jks")
            keyAlias "PADumper"
            storePassword "012345"
            keyPassword "012345"
        }
        release {
            storeFile file("keystore.jks")
            keyAlias "PADumper"
            storePassword "012345"
            keyPassword "012345"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    kotlinOptions {
        jvmTarget = '17'
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.5.4"
    }
}

dependencies {
    //Ui
    def composeBom = platform('androidx.compose:compose-bom:2023.10.01')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended:1.5.4'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.8.2'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2'
    implementation "androidx.compose.runtime:runtime-livedata:1.5.4"
    // Integration with Navigation
    implementation 'androidx.navigation:navigation-compose:2.7.6'

    //Root
    def libsuVersion = '5.2.0'
    implementation "com.github.topjohnwu.libsu:core:$libsuVersion"
    implementation "com.github.topjohnwu.libsu:service:$libsuVersion"

    //Storage Helper
    implementation "com.anggrayudi:storage:1.5.5"
}
